; *******************************************************************
; *** This software is copyright 2005 by Michael H Riley          ***
; *** You have permission to use, modify, copy, and distribute    ***
; *** this software so long as this copyright notice is retained. ***
; *** This software may not be used in commercial applications    ***
; *** without express written permission from the author.         ***
; *******************************************************************

; #define ELF2K

#ifdef ELF2K
#define SERP    b3
#define SERN    bn3
#else
#define SERP    b2
#define SERN    bn2
#endif

data:   equ     0
scall:  equ     r4
sret:   equ     r5

#ifdef ELF2K
           org     0f500h
; *******************************************
; *** Implement RE.1 for serial selection ***
; *******************************************
e2k_brk:   ghi     re                  ; get baud constant
           lbnz    f_btest             ; if non-zero, then bit-banged
           lbr     f_utest             ; otherwise use UART

e2k_tx:    plo     re                  ; save character
           ghi     re                  ; get baud constant
           lbz     e2k_tx1             ; jump if UART
           glo     re                  ; recover character
           lbr     f_btype             ; jump to bit-banged code
e2k_tx1:   glo     re                  ; recover character
           lbr     f_utype             ; jump to UART routine

e2k_rx:    ghi     re                  ; get baud constant
           lbnz    f_bread             ; jump to bit banged code
           lbr     f_uread             ; jump to UART code

; ****************************************************
; *** Test to see if character available from UART ***
; *** Returns: DF=1 - character available          ***
; ****************************************************
e2k_utest: ldi     015h                ; need UART line status register
           str     r2                  ; prepare for out
           out     2                   ; write to register select port
           dec     r2                  ; back to a free spot
           inp     3                   ; read line status register
           shr                         ; shift data ready bit into DF
           sep     sret                ; and return

; ************************************
; *** Read byte from Elf 2000 UART ***
; *** Returns: D - byte read       ***
; ************************************
e2k_uread: ldi     015h                ; need UART line status register
           str     r2                  ; prepare for out
           out     2                   ; write to register select port
           dec     r2                  ; correct for inc on out
uread_lp:  inp     3                   ; read line status register
           ani     1                   ; mask for data ready bit
           lbz     uread_lp            ; loop back if no bytes to read
           ldi     010h                ; select data register
           str     r2                  ; prepare for out
           out     2                   ; write to register select port
           dec     r2                  ; back to free spot
           inp     3                   ; read UART data register
           sep     sret                ; return byte to caller

; ***********************************
; *** Write byte to Elf 2000 UART ***
; *** D - byte to write           ***
; ***********************************
e2k_utype: stxd                        ; save byte to send onto stack
           ldi     015h                ; need line status register of UART
           str     r2                  ; write to stack
           out     2                   ; select UART port
           dec     r2                  ; keep stack at empty spot
utype_lp:  inp     3                   ; read status port
           ani     020h                ; mask for holding register empty
           lbz     utype_lp            ; loop until allowd to send character
           ldi     010h                ; select data register
           str     r2                  ; prepare for out
           out     2                   ; select UART register
           out     3                   ; write byte to UART
           dec     r2                  ; correct for inc on OUT
           sep     sret                ; and return to caller

; *****************************************
; *** Setup UART parameters             ***
; *** D = Comm parameters               ***
; ***  Bits 0-2 = 0 0 0 - 300 baud      ***
; ***             0 0 1 - 1200 baud     ***
; ***             0 1 0 - 2400 baud     ***
; ***             0 1 1 - 4800 baud     ***
; ***             1 0 0 - 9600 baud     ***
; ***             1 0 1 - 19200 baud    ***
; ***             1 1 0 - 38400 baud    ***
; ***             1 1 1 - 76800 baud    ***
; ***  bit 3 - reserved for future baud ***
; ***          rates                    ***
; ***  Bits 4-5 = 0 0 - 5 bit words     ***
; ***             0 1 - 6 bit words     ***
; ***             1 0 - 7 bit words     ***
; ***             1 1 - 8 bit words     ***
; ***  Bit    6 =   0 - no parity       ***
; ***               1 - parity enabled  ***
; ***  Bit    7 -   0 - odd parity      ***
; ***               1 - even parity     ***
; *** Returns: DF=0 - No UART installed ***
; ***          DF=1 - success           ***
; *****************************************
e2k_stbd:  plo     re                  ; save a coyp of parameters
           sex     r3                  ; setup line control register of UART
           out     2
           db      013h
           sex     r2                  ; set x back to stack
           xri     0ffh                ; check for existance of UART
           lbz     no_uart             ; jump if not installed
           xri     0ffh                ; reset value
           ori     080h                ; set DLAB bit
           str     r2                  ; store for write
           out     3                   ; write line control register
           dec     r2                  ; compensate for out
           dec     r2                  ; need some workspace
           glo     re                  ; get com parameters
           ani     7                   ; strip all but baud rate bits
           shl                         ; multiply by 2
           str     r2                  ; and prepare for add
           ldi     low baudtab         ; point to diviser table
           add                         ; add in offset
           plo     rf                  ; place into rf
           ldi     high baudtab        ; high portion of address
           adi     0                   ; propagate carry
           phi     rf
           sex     r3                  ; setup for output
           out     2                   ; select msb of diviser latch
           db      011h
           sex     rf                  ; point to diviser value
           out     2                   ; and write to latch
           sex     r3                  ; setup for next register select
           out     2                   ; select lsb of diviser latch
           db      010h
           sex     rf                  ; set x to diviser table
           out     3                   ; and write to uart
           sex     r2                  ; set x back to stack
           inc     r2                  ; point back to line control value
           glo     re                  ; recover comm parameters
           shr                         ; shift out baud value
           shr
           shr
           shr
           ani     1fh                 ; clear unneded bits
           str     r2                  ; prepare for out
           sex     r3                  ; need to write register select
           out     2                   ; select line control register
           db      013h
           sex     r2                  ; X back to stack
           out     3                   ; write new line control register
           dec     r2                  ; compensate for out
           smi     0                   ; signal success
           sep     sret                ; return to caller
no_uart:   adi     0                   ; signal no uart installed
           sep     sret                ; and return
baudtab:   dw      512,128,64,32,16,8,4

; *********************************
; *** Test if RTC is present    ***
; *** Returns: DF=1 present     ***
; ***          DF=0 not present ***
; ***          d = size         ***
; *********************************
rtctest:   sex     r3                  ; setup X
           out     2                   ; select first general register
           db      63+080h
           sex     r2                  ; reset X to stack
           inp     3                   ; read it
           stxd                        ; keep a copy
           xri     0ffh                ; invert the bits
           str     r2                  ; and write it back
           out     3
           dec     r2                  ; setup to read it again
           inp     3                   ; read it
           irx                         ; point back to first value
           sm                          ; and compare
           lbz     rtc_no              ; jump if value did not change
           out     3                   ; write original value back to rtc
           dec     r2                  ; compensate for inc on out
; **************************************
; *** Check if battery is still good ***
; **************************************
           sex     r3                  ; setup for immediate out
           out     2                   ; select register d
           db      8bh
           sex     r2                  ; reset stack pointer
           inp     3                   ; and read register D
           ani     80h                 ; check high bit
           lbz     rtc_no              ; jump if invalid
; **************************************
; *** now check for 128 byte version ***
; **************************************
           sex     r3                  ; setup X
           out     2                   ; select first general register
           db      127+080h
           sex     r2                  ; reset X to stack
           inp     3                   ; read it
           stxd                        ; keep a copy
           xri     0ffh                ; invert the bits
           str     r2                  ; and write it back
           out     3
           dec     r2                  ; setup to read it again
           inp     3                   ; read it
           irx                         ; point back to first value
           sm                          ; and compare
           lbz     rtc_64              ; jump if value did not change
           out     3                   ; write original value back to rtc
           dec     r2                  ; compensate for inc on out
           smi     0                   ; signal RTC present
           ldi     128                 ; signal 128 byte version
           sep     sret                ; and return
rtc_64:    smi     0                   ; signal RTC present
           ldi     64                  ; signal 64 byte version
           sep     sret                ; and return
rtc_no:    adi     0                   ; signal RTC is not present
           sep     sret                ; and return

; ********************************
; *** Read RTC date            ***
; *** RF - buffer for time     ***
; *** Returns: DF=1 - success  ***
; ***          DF=0 - error    ***
; ***             D=0 no RTC   ***
; ***             D=1 bad time ***
; ********************************
e2k_gtod:  sep     scall               ; see if RTC is present
           dw      rtctest
           lbdf    rtc_go              ; jump if RTC was found
rtc_err:   adi     0                   ; signal error
           ldi     0                   ; as no RTC
           sep     sret                ; return
rtc_go:    sex     r3                  ; setup for immediate out
           out     2                   ; select RTC register B
           db      08bh
           sex     r2                  ; reset stack
           inp     3                   ; read register
           ori     6                   ; set binary and 24 hour modes
           str     r2                  ; save for out
           sex     r3                  ; setup for immediate out
           out     2                   ; select RTC register B
           db      08bh
           sex     r2                  ; point back to stack
           out     3                   ; and write new register B value
           dec     r2                  ; account for inc on out
           glo     r7                  ; save consumed register
           stxd
           ghi     r7
           stxd
           ldi     high date_tab       ; point to date table
           phi     r7
           ldi     low date_tab
           plo     r7
date_lp:   sex     r7                  ; select register
           out     2
           sex     rf                  ; set to receive buffer
           inp     3                   ; read month
           sex     r2                  ; reset stack
           sm                          ; check for valid range
           lbdf    e2k_baddt           ; jump if invalid
           inc     rf                  ; point to next space in buffer
           inc     r7                  ; point to next register
           ldn     r7                  ; need to see if done
           lbnz    date_lp             ; loop back for next
           smi     0                   ; indicate valid read
e2k_gt_dn: plo     re                  ; save D
           irx                         ; recover consumed register
           ldxa
           phi     r7
           ldx
           plo     r7
           glo     re                  ; recover D
           sep     sret                ; return to caller
e2k_baddt: ldi     1                   ; signal bad time/date
           adi     0                   ; signal error
           lbr     e2k_gt_dn           ; finish up
date_tab:  db      088h,13,087h,32,089h,99,084h,24,082h,60,080h,60,0

; ********************************
; *** Set RTC date             ***
; *** RF - buffer holding data ***
; *** Returns: DF=1 - success  ***
; ***          DF=0 - no RTC   ***
; ********************************
e2k_stod:  sep     scall               ; see if RTC is present
           dw      rtctest
           lbnf    rtc_err             ; jump if no RTC is present
           sex     r3                  ; setup for immediate out
           out     2                   ; select RTC register B
           db      08bh
           sex     r2                  ; reset stack
           inp     3                   ; read register
           ori     6                   ; set binary and 24 hour modes
           str     r2                  ; save for out
           sex     r3                  ; setup for immediate out
           out     2                   ; select RTC register B
           db      08bh
           sex     r2                  ; point back to stack
           out     3                   ; and write new register B value
           dec     r2                  ; account for inc on out
           glo     r7                  ; save consumed register
           stxd
           ghi     r7
           stxd
           ldi     high date_tab       ; point to date table
           phi     r7
           ldi     low date_tab
           plo     r7
stod_lp:   sex     r7                  ; set x to date table
           out     2                   ; select RTC register
           inc     r7                  ; move past validator
           sex     rf                  ; set x to data buffer
           out     3                   ; write byte to rtc
           ldn     r7                  ; get next register value
           lbnz    stod_lp             ; loop if more to go
           sex     r2                  ; set X back to stack 
           smi     0                   ; signall success
           lbr     e2k_gt_dn           ; finish up

; ************************************************
; *** Read bytes from NVR                      ***
; *** RF.0 - relative address to start reading ***
; *** RD - Destination for bytes               ***
; *** RC.0 - count                             ***
; *** Returns: DF=1 - success                  ***
; ***          DF=0 - no NVR                   ***
; ************************************************
e2k_rdnvr: sep     scall               ; check for presence of NVR
           dw      rtctest
           lbnf    rtc_err             ; jujp if not
           glo     rf                  ; save RF
           stxd
           ghi     rf
           stxd
           sep     scall               ; compute checksum
           dw      nvr_chk
           sep     scall               ; get current checksum
           dw      get_chk
           glo     rd                  ; compare them
           str     r2
           glo     rf
           sm
           lbnz    chk_err             ; jump on checksum error
           ghi     rd                  ; check high byte as well
           str     r2
           ghi     rf
           sm
           lbnz    chk_err
           irx                         ; recover RF
           ldxa
           phi     rf
           ldx
           plo     rf
           glo     rf                  ; get starting register
           adi     0eh                 ; offset to beginning of general regs.
           str     r2                  ; store into memory
rdnvr_lp:  glo     rc                  ; see if done
           lbz     rdnvr_dn            ; jump if so
           out     2                   ; write next register address
           dec     r2                  ; compensate for inc on out
           sex     rd                  ; set x to destination memory
           inp     3                   ; read NVR
           inc     rd                  ; increment destination
           dec     rc                  ; decrement count
           sex     r2                  ; point x back to stack
           lbr     rdnvr_lp            ; loop back for more
rdnvr_dn:  smi     0                   ; signal success
           sep     sret                ; and return
chk_err:   irx                         ; recover RF
           ldxa
           phi     rf
           ldx
           plo     rf
           adi     0                   ; signal an error
           ldi     1                   ; signal bad checksum
           sep     sret                ; return to caller

; ************************************************
; *** Write bytes to NVR                       ***
; *** RF.0 - relative address to start writing ***
; *** RD - source for bytes                    ***
; *** RC.0 - count                             ***
; *** Returns: DF=1 - success                  ***
; ***          DF=0 - no NVR                   ***
; ************************************************
e2k_wrnvr: sep     scall               ; check for presence of NVR
           dw      rtctest
           lbnf    rtc_err             ; jujp if not
           glo     rf                  ; get starting register
           adi     0eh                 ; offset to beginning of general regs.
           str     r2                  ; store into memory
wrnvr_lp:  glo     rc                  ; see if done
           lbz     wrnvr_dn            ; jump if so
           out     2                   ; write next register address
           dec     r2                  ; compensate for inc on out
           sex     rd                  ; set x to source memory
           out     3                   ; write NVR
           dec     rc                  ; decrement count
           sex     r2                  ; point x back to stack
           lbr     wrnvr_lp            ; loop back for more
wrnvr_dn:  sep     scall               ; compute new checksum
           dw      nvr_chk
           sep     scall               ; and write to NVR
           dw      set_chk
           smi     0                   ; signal success
           sep     sret                ; and return

; ************************************
; *** Compute checksum for NVR ram ***
; *** Returns: RF - checksum       ***
; ************************************
nvr_chk:   ldi     0                   ; set initial checksum
           phi     rf
           plo     rf
           ldi     08eh                ; starting register
           dec     r2                  ; place into memory
           str     r2
           dec     r2
           sep     scall               ; get size of nvram
           dw      rtctest
           inc     r2
           smi     2                   ; minus 2 bytes for checksum
           ori     80h                 ; setup end
           plo     rc
nvr_chklp: out     2                   ; select NVR address
           inp     3                   ; read it
           glo     rf                  ; add into checksum
           add
           plo     rf
           ghi     rf                  ; propagate carry
           adi     0
           phi     rf
           shl                         ; now ring shift left
           glo     rf
           shlc
           plo     rf
           ghi     rf
           shlc
           phi     rf
           dec     r2                  ; point to register address
           ldn     r2                  ; retrieve it
           adi     1                   ; point to next register
           str     r2                  ; store again
           glo     rc                  ; get end
           sm                          ; see if at end
           lbnz    nvr_chklp           ; loop back if not
           inc     r2                  ; move to correct stack position
           sep     sret                ; and return

; ************************
; *** Set NVR checksum ***
; *** RF - checkshum   ***
; ************************
set_chk:   glo     rc                  ; save consumed register
           stxd
           sep     scall               ; get size of nvram
           dw      rtctest
           smi     2                   ; msb of checksum
           plo     rc
           glo     rc                  ; setup address
           ori     080h
           str     r2                  ; store for out
           out     2                   ; write register selection port
           dec     r2                  ; compensate for increment
           ghi     rf                  ; get high of checksum
           str     r2                  ; prepare for out
           out     3                   ; write to NVRAM
           inc     rc                  ; next position
           glo     rc                  ; setup address
           ori     080h
           str     r2                  ; store for out
           out     2                   ; write register selection port
           dec     r2                  ; compensate for increment
           glo     rf                  ; get low of checksum
           str     r2                  ; prepare for out
           out     3                   ; write to NVRAM
           irx                         ; recover consumed register
           ldx
           plo     rc
           sep     sret                ; and return

; ******************************
; *** Get NVR checksum       ***
; *** Returns: RD - checksum ***
; ******************************
get_chk:   glo     rc                  ; save consumed register
           stxd
           sep     scall               ; get size of nvram
           dw      rtctest
           smi     2                   ; msb of checksum
           plo     rc
           glo     rc                  ; get checksum address
           ori     080h                ; setup address
           str     r2                  ; store for out
           out     2                   ; compensate for inc
           inp     3                   ; read NVR
           phi     rd                  ; put into RD.1
           inc     rc                  ; move to next address
           glo     rc                  ; get checksum address
           ori     080h                ; setup address
           str     r2                  ; store for out
           out     2                   ; compensate for inc
           inp     3                   ; read NVR
           plo     rd                  ; put into RD.0
           irx                         ; recover consumed register
           ldx
           plo     rc
           sep     sret                ; return to caller

; ********************************
; *** Select master ide device ***
; ********************************
master:    glo     rc                  ; save consumed register
           stxd
           ghi     rc
           stxd
           ldi     high waitrdy        ; get address of subs
           phi     rc                  ; place into rf
           ldi     low waitrdy
           plo     rc
           sex     r2                  ; point x to stack
           mark                        ; save current P and X
           sep     rc                  ; call wait for RDY routine
           dec     r2                  ; compensate for RET increment
           sex     r3                  ; setup for immediate outs
           out     2                   ; select device register
           db      6
           out     3                   ; now output code for master
           db      0
           sex     r2                  ; restore x register
           irx                         ; recover consumed register
           ldxa
           phi     rc
           ldx
           plo     rc
           sep     sret                ; and return

; *******************************
; *** Select slave ide device ***
; *******************************
slave:     glo     rc                  ; save consumed register
           stxd
           ghi     rc
           stxd
           ldi     high waitrdy        ; get address of subs
           phi     rc                  ; place into rf
           ldi     low waitrdy
           plo     rc
           sex     r2                  ; point x to stack
           mark                        ; save current P and X
           sep     rc                  ; call wait for RDY routine
           dec     r2                  ; compensate for RET increment
           sex     r3                  ; setup for immediate outs
           out     2                   ; select device register
           db      6
           out     3                   ; now output code for slave
           db      010h
           sex     r2                  ; restore x register
           irx                         ; recover consumed register
           ldxa
           phi     rc
           ldx
           plo     rc
           sep     sret                ; and return

; ************************************
; *** get ide identity information ***
; *** RF - pointer to buffer       ***
; *** RD.0 = 0 master              ***
; ***      = 1 slave               ***
; ************************************
ide_ident: glo     rf                  ; save buffer position
           stxd
           ghi     rf
           stxd
           sep     scall               ; read drive data
           dw      ide_id
           irx                         ; recover buffer
           ldxa
           phi     rf
           ldx
           plo     rf
           lbr     swap                ; perform byte swaps
          
ide_id:    glo     rd
           lbz     id_master
           sep     scall               ; select slave drive
           dw      slave
           lbr     ident_go
id_master: sep     scall               ; select master
           dw      master
ident_go:  glo     rc                  ; save consumed registers
           stxd
           ghi     rc
           stxd
           glo     r7
           stxd
           ghi     r7
           stxd
           ldi     high waitrdy        ; get address of subs
           phi     rc                  ; place into rf
           ldi     low waitrdy
           plo     rc
           sex     r2                  ; point x to stack
           mark                        ; save current P and X
           sep     rc                  ; call wait for RDY routine
           dec     r2                  ; compensate for RET increment
           sex     r3                  ; setup for outs
           out     2                   ; select command port
           db      7
           out     3                   ; issue get identity command
           db      0ech
           sex     r2                  ; reset stack
           lbr     ide_read            ; read drive data

swap:      ldi     0                   ; setup counter
           plo     rc
swap_lp:   lda     rf                  ; get first byte
           plo     re                  ; keep a copy
           ldn     rf                  ; get second byte
           phi     rc                  ; keep a copy
           glo     re                  ; get first byte
           str     rf                  ; write into 2nd position
           dec     rf                  ; point back to first byte
           ghi     rc                  ; get 2nd byte
           str     rf                  ; write into first position
           inc     rf                  ; point to next word
           inc     rf
           dec     rc                  ; decrement count
           glo     rc                  ; see if done
           lbnz    swap_lp             ; loop back if not
           sep     sret                ; otherwise return to caller

; *****************************************
; *** Determine size of hard drive      ***
; *** RD.0 = 0 - master drive           ***
; *** RD.0 = 1 - slave drive            ***
; *** Returns: RF - size of drive in MB ***
; ***               0=no drive          ***
; *****************************************
ide_size:  glo     rd                  ; get selected drive
           lbz     sz_master           ; jump if master is selected
           sep     scall               ; select slave drive
           dw      slave
           lbr     size_go             ; then continue
sz_master: sep     scall               ; select master drive
           dw      master
size_go:   sex     r3                  ; setup for immediate out
           out     2                   ; select status register
           db      07h
           sex     r2                  ; reset stack
           ldi     0                   ; setup for failure count
           plo     rc
           phi     rc
rdyloopz:  inp     3                   ; read status port
           ani     0c0h                ; mask for BSY and RDY
           smi     040h                ; want only RDY bit
           lbz     sz_ready            ; jump if drive is ready
           dec     rc                  ; decrement timetout
           glo     rc                  ; check if timeout occurred
           lbnz    rdyloopz            ; jump if not
           ghi     rc
           lbnz    rdyloopz
           ldi     0                   ; signify no drive
           phi     rf
           plo     rf
           sep     sret                ; and return
sz_ready:  sex     r3                  ; issued commands to perform an ident
           out     2                   ; select command register
           db      07h
           out     3                   ; issue identify command
           db      0ech
           sex     r2                  ; pont x back to stack
zdrqloop:  inp     3                   ; read status register
           ani     8                   ; mask for DRQ bit
           bz      zdrqloop            ; loop until found
           sex     r3                  ; now setup data register
           out     2
           db      0
           sex     r2                  ; x back to stack
           ldi     120                 ; need to throw away 120 bytes
           plo     rc
size_lp1:  inp     3                   ; read byte from drive
           dec     rc                  ; decrement count
           glo     rc                  ; see if done
           lbnz    size_lp1            ; loop back if not
           inp     3                   ; read 4 bytes into r8:r7
           plo     r7
           inp     3
           phi     r7
           inp     3
           plo     r8
           inp     3
           phi     r8
           ldi     1                   ; need to read 388 more bytes
           phi     rc
           ldi     132
           plo     rc
size_lp2:  inp     3                   ; read byte
           dec     rc                  ; decrement count
           glo     rc                  ; see if done
           lbnz    size_lp2
           ghi     rc                  ; check high byte
           lbnz    size_lp2
           ghi     r7                  ; need 11 shift to convert to MB
           plo     r7
           glo     r8
           phi     r7
           ghi     r8
           plo     r8                  ; 8 shift are now done
           ldi     3                   ; need 3 more
           plo     rc
size_lp3:  glo     r8                  ; shift whole number right by 1
           shr
           plo     r8
           ghi     r7
           shrc
           phi     r7
           glo     r7
           shrc
           plo     r7
           dec     rc                  ; decrement count
           glo     rc                  ; see if done
           lbnz    size_lp3            ; jump if not
           ghi     r7                  ; transfer result
           phi     rf
           glo     r7
           plo     rf
           sep     sret                ; and return

#endif

           org     0f800h
f_bread:   lbr     read
f_btype:   lbr     type
f_btest:   lbr     brktest
#ifdef ELF2K
f_utype:   lbr     e2k_utype
f_uread:   lbr     e2k_uread
f_utest:   lbr     e2k_utest
f_usetbd:  lbr     e2k_stbd
f_gettod:  lbr     e2k_gtod
f_settod:  lbr     e2k_stod
f_rdnvr:   lbr     e2k_rdnvr
f_wrnvr:   lbr     e2k_wrnvr
f_idesize: lbr     ide_size
f_ideid:   lbr     ide_ident
#else
f_utype:   lbr     err
f_uread:   lbr     err
f_utest:   lbr     err
f_usetbd:  lbr     err
f_gettod:  lbr     err
f_settod:  lbr     err
f_rdnvr:   lbr     err
f_wrnvr:   lbr     err
f_idesize: lbr     err
f_ideid:   lbr     err
#endif

; *** rf - pointer to ascii string
; *** returns: rf - first non-numeric character
; ***          RD - number
; ***          DF = 1 if first character non-numeric 
atoi:      ldi     0                   ; clear answer
           phi     rd
           plo     rd
           plo     re                  ; signify positive number
           ldn     rf                  ; get first value
           sep     scall               ; check if numeric
           dw      isnum
           bdf     atoicnt             ; jump if so
           xri     '-'                 ; check for minus
           bz      atoicnt             ; jump if so
           smi     0                   ; signal number error
           sep     sret                ; return to caller
atoicnt:   ldn     rf                  ; get first bytr
           xri     '-'                 ; check for negative
           bnz     atoilp              ; jump if not negative
           ldi     1                   ; signify negative number
           plo     re
           inc     rf                  ; move past - sign
atoilp:    ldn     rf                  ; get byte from input
           sep     scall               ; check for number
           dw      isnum
           lbnf    atoidn              ; jump if not
           ghi     rd                  ; make a copy for add
           stxd
           glo     rd                  ; multiply by 2
           str     r2                  ; TOS now has copy of number
           shl
           plo     rd
           ghi     rd
           shlc
           phi     rd
           glo     rd                  ; multiply by 4
           shl
           plo     rd
           ghi     rd
           shlc
           phi     rd
           glo     rd                  ; multiply by 5 (add TOS)
           add
           plo     rd
           irx                         ; point to msb
           ghi     rd
           adc
           phi     rd
           glo     rd                  ; multiply by 10
           shl
           plo     rd
           ghi     rd
           shlc
           phi     rd
           lda     rf                  ; get byte from buffer
           smi     '0'                 ; convert to binary
           str     r2                  ; prepare for addition
           glo     rd                  ; add in new digit
           add
           plo     rd
           ghi     rd
           adci    0
           phi     rd
           br      atoilp              ; loop back for next character
atoidn:    adi     0                   ; signal valid number
           sep     sret                ; return to caller

; **** convert binary number to ascii
; **** RD - number to convert
; **** RF - buffer to store
; **** Returns: RF - last postion+1
uintout:   br      positive
intout:    sex     r2                  ; point X to stack
           ghi     rd                  ; get high of number
           ani     128                 ; mask all bit sign bit
           bz      positive            ; jump if number is positive
           ldi     '-'                 ; need a minus sign
           str     rf                  ; store into output
           inc     rf
           glo     rd                  ; get low byte
           str     r2                  ; store it
           ldi     0                   ; need to subtract from 0
           sm
           plo     rd                  ; put back
           ghi     rd                  ; get high byte
           str     r2                  ; place into memory
           ldi     0                   ; still subtracting from zero
           smb     
           phi     rd                  ; and put back 
positive:  glo     r7                  ; save consumed registers
           stxd
           ghi     r7
           stxd
           glo     r8                  ; save consumed registers
           stxd
           ghi     r8
           stxd
           glo     r9                  ; save consumed registers
           stxd
           ghi     r9
           stxd
           ldi     high numbers        ; point to numbers
           phi     r9
           ldi     low numbers
           plo     r9
           lda     r9                  ; get first division
           phi     r7
           lda     r9
           plo     r7
           ldi     0                   ; leading zero flag
           stxd                        ; store onto stack
nxtiter:   ldi     0                   ; star count at zero
           plo     r8                  ; place into low of r8
divlp:     glo     r7                  ; get low of number to subtrace
           str     r2                  ; place into memory
           glo     rd                  ; get low of number
           sm                          ; subtract
           phi     r8                  ; place into temp space
           ghi     r7                  ; get high of subtraction
           str     r2                  ; place into memory
           ghi     rd                  ; get high of number
           smb                         ; perform subtract
           bnf     nomore              ; jump if subtraction was too large
           phi     rd                  ; store result
           ghi     r8
           plo     rd
           inc     r8                  ; increment count
           br      divlp               ; and loop back
nomore:    irx                         ; point back to leading zero flag
           glo     r8
           bnz     nonzero             ; jump if not zero
           ldn     r2                  ; get flag
           bnz     allow0              ; jump if no longer zero
           dec     r2                  ; keep leading zero flag
           br      findnxt             ; skip output
allow0:    ldi     0                   ; recover the zero
nonzero:   adi     30h                 ; convert to ascii
           str     rf                  ; store into buffer
           inc     rf
           ldi     1                   ; need to set leading flag
           stxd                        ; store it
findnxt:   dec     r7                  ; subtract 1 for zero check
           glo     r7                  ; check for end
           bz      intdone             ; jump if done
           lda     r9                  ; get next number
           phi     r7
           lda     r9
           plo     r7
           smi     1                   ; see if at last number
           bnz     nxtiter             ; jump if not 
           irx                         ; set leading flag
           ldi     1
           stxd
           br      nxtiter
intdone:   irx                         ; put x back where it belongs
           irx                         ; recover consumed registers
           ldxa
           phi     r9
           ldxa
           plo     r9
           ldxa
           phi     r8
           ldxa
           plo     r8
           ldxa
           phi     r7
           ldx
           plo     r7
           sep     sret                ; return to caller

numbers:   db      027h,010h,3,0e8h,0,100,0,10,0,1


           org     0f900h
buffer:    equ     03
minimon:   ldi     high start          ; setup main pc
           phi     r6
           ldi     low start
           plo     r6
           ldi     0                   ; setup stack
           phi     r2
           ldi     0ffh
           plo     r2
           sex     r2
           lbr     f_initcall
start:     sep     scall               ; initialize baud setting
           dw      f_setbd

mainlp:    ldi     high prompt         ; get address of prompt
           phi     rf
           ldi     low prompt
           plo     rf
           sep     scall               ; display prompt
           dw      f_msg
           sep     scall
           dw      loadbuf
           sep     scall               ; get input from user
           dw      f_input
           sep     scall
           dw      docrlf
           sep     scall
           dw      loadbuf
           lda     rf                  ; get first byte
           phi     rc                  ; save it
           sep     scall               ; obtain address
           dw      f_hexin
           ghi     rd                  ; move to address var
           phi     ra
           glo     rd
           plo     ra

           ghi     rc                  ; retrieve command
           smi     33
           bz      storesp
           smi     28                  ; look for copy command
           bz      copy                ; jump if found
           smi     2
           bz      examine
           smi     1
           bnz     mainlp
run:       ghi     ra                  ; move to address var
	   phi     r0
	   glo     ra
	   plo     r0
           sex     r0
           sep     r0
           
examine:   ldi     8                   ; set count to 128 bytes
           plo     rc
exloop1:   ghi     ra                  ; get address
           phi     rd                  ; transfer for output
           glo     ra
           plo     rd
           sep     scall
           dw      loadbuf
           sep     scall               ; put address into output
           dw      f_hexout4
           ldi     ':'                 ; want a colon
           str     rf
           inc     rf
           ldi     16                  ; 16 bytes per line
           plo     rb                  ; put into secondary counter
exloop2:   ldi     ' '                 ; output a space
           str     rf
           inc     rf
           lda     ra                  ; get next byte from memory
           plo     rd                  ; prepare for output
           sep     scall               ; convert for output
           dw      f_hexout2
           dec     rb                  ; decrment line count
           glo     rb                  ; get count
           bnz     exloop2             ; loop back if not done
           ldi     0                   ; need terminator
           str     rf
           sep     scall
           dw      loadbuf
           sep     scall               ; output the line
           dw      f_msg
           sep     scall
           dw      docrlf
           dec     rc                  ; decrement line count
           glo     rc                  ; get count
           bnz     exloop1             ; loop back if not all lines printed
           br      mainlp              ; return to main loop

storesp:   ldn     rf                  ; get byte from input
           bz      mainlp              ; jump if found
           smi     33                  ; check for less than space
           bdf     storec              ; jump if not space
           inc     rf                  ; point to next character
           br      storesp             ; and keep moving past spaces
storec:    sep     scall               ; convert next number
           dw      f_hexin
           glo     rd                  ; get converted byt
           str     ra                  ; store into memory
           inc     ra                  ; point to next position
           br      storesp             ; and do next character

copy:      ghi     ra                  ; move source
           phi     r8
           glo     ra
           plo     r8
           sep     scall               ; move past spaces
           dw      f_ltrim
           sep     scall               ; get destination address
           dw      f_hexin
           ghi     rd                  ; transfer to r9
           phi     r9
           glo     rd
           plo     r9
           sep     scall               ; move past spaces
           dw      f_ltrim
           sep     scall               ; get source address
           dw      f_hexin
           ghi     rd                  ; transfer to rc
           phi     rc
           glo     rd
           plo     rc
movelp:    ghi     rc                  ; check for zero
           bnz     domove
           glo     rc
           bnz     domove
           br      done
domove:    lda     r8
           str     r9
           inc     r9
           dec     rc
           br      movelp
done:      br      mainlp

loadbuf:   ldi     high buffer
           phi     rf
           ldi     low buffer
           plo     rf
           sep     sret

docrlf:    ldi     high crlf
           phi     rf
           ldi     low crlf
           plo     rf
           sep     scall
           dw      f_msg
           sep     sret

prompt:    db      '>',0
crlf:      db      10,13,0

; ***************************************
; *** Type inline message             ***
; ***************************************
typeinmsg: lda     r6                  ; load byte from message
           lbz     return              ; return if last byte
           sep     scall               ; call type routine
           dw      f_type
           br      typeinmsg           ; loop until a zero found

; *************************************
; *** Check if character is numeric ***
; *** D - char to check             ***
; *** Returns DF=1 if numeric       ***
; ***         DF=0 if not           ***
; *************************************
isnum:     plo     re                  ; save a copy
           smi     '0'                 ; check for below zero
           bnf     fails               ; jump if below
           smi     10                  ; see if above
           bdf     fails               ; fails if so
passes:    smi     0                   ; signal success
           lskp
fails:     adi     0                   ; signal failure
           glo     re                  ; recover character
           sep     sret                ; and return

err:       smi     0                   ; signal an error
           sep     sret                ; and return

           org   0fa00h
; ***************************************************************
; *** Function to convert hex input characters to binary      ***
; *** RF - Pointer to characters                              ***
; *** Returns - RF - First character that is not alphanumeric ***
; ***           RD - Converted number                         ***
; ***************************************************************
hexin:     ldi     0                   ; set initial total
           phi     rd
           plo     rd
tobinlp:   lda     rf                  ; get input character
           smi     '0'                 ; convert to binary
           bnf     tobindn             ; jump if termination
           stxd
           ani     0f0h                ; check for alpha
           irx                         ; point back
           bz      isnumeric
           ldx                         ; recover byte
           smi     49                  ; see if lowercase
           bnf     hexgo
           ldx                         ; get byte
           smi     32                  ; convert to uppercase
           br      hexgo2              ; and continue
hexgo:     ldx                         ; recover byte
hexgo2:    smi     7                   ; offset
           br      tobingo             ; and continue
isnumeric: ldx                         ; recover byte
           smi     10                  ; check for end of numbers
           bdf     tobindn             ; jump if end
           ldx                         ; recover byte
tobingo:   stxd                        ; save number
           smi     16                  ; check for valid range
           bnf     tobingd             ; jump if good
           irx                         ; remove number from stack
           br      tobindn
tobingd:   ldi     4                   ; need to multiply by 16
tobinglp:  stxd
           glo     rd                  ; multiply by 2
           shl
           plo     rd
           ghi     rd
           shlc
           phi     rd
           irx
           ldi     1
           sd
           bnz     tobinglp
           irx                         ; point to new number
           glo     rd                  ; and add to total
           add
           plo     rd
           ghi     rd
           adci    0
           phi     rd
           br      tobinlp             ; loop back for next character
tobindn:   dec     rf                  ; move back to terminating character
           sep     sret                ; return to caller

; *********************************************
; *** Convert a binary number to hex output ***
; *** RD - Number to convert                ***
; *** RF - Buffer for output                ***
; *** Returns: RF - next buffer position    ***
; ***          RD - consumed                ***
; *********************************************
hexout2:   glo     rd                  ; move low byte to high
           phi     rd
           ldi     2                   ; 2 nybbles to display
           lskp                        ; skip over the 4
hexout4:   ldi     4                   ; 4 nybbles to display
hexoutlp:  stxd                        ; save the count
           ldi     0                   ; zero the temp var
           plo     re
           ldi     4                   ; perform 4 shift
hexoutl2:  stxd                        ; save count
           glo     rd                  ; perform shift
           shl
           plo     rd
           ghi     rd
           shlc
           phi     rd
           glo     re
           shlc
           plo     re
           irx                         ; point back to count
           ldi     1                   ; need to decrement it
           sd
           bnz     hexoutl2            ; jump if more shifts needed
           glo     re                  ; get nybble
           smi     10                  ; compare to 10
           bdf     hexoutal            ; jump if alpha
           glo     re                  ; get value
           adi     30h                 ; convert to ascii
hexoutl3:  str     rf                  ; store value into buffer
           inc     rf
           irx                         ; point to count
           ldi     1                   ; need to subtract 1 from it
           sd
           bnz     hexoutlp            ; loop if not done
           sep     sret                ; return to caller
hexoutal:  glo     re                  ; get value
           adi     55                  ; convert to ascii
           br      hexoutl3            ; and continue


         sep     r3                    ; jump to called routine
call:    plo     re                    ; Save D
         ghi     r6                    ; save last R[6] to stack
         sex     r2
         stxd
         glo     r6
         stxd
         ghi     r3                    ; copy R[3] to R[6]
         phi     r6
         glo     r3
         plo     r6
         lda     r6                    ; get subroutine address
         phi     r3                    ; and put into r3
         lda     r6
         plo     r3
         glo     re                    ; recover D
         br      call-1                ; transfer control to subroutine

         sep     r3                    ; transfer control back to coller
ret:     plo     re                    ; Save D
         ghi     r6                    ; copy R[6] to R[3]
         phi     r3
         glo     r6
         plo     r3
         sex     r2
         irx                           ; point to old R[6]
         ldxa
         plo     r6
         ldx
         phi     r6
         glo     re
         br      ret-1                 ; and perform return to caller

; ********************************
; *** See if D is alphabetic   ***
; *** Returns DF=0 - not alpha ***
; ***         DF=1 - is alpha  ***
; ********************************
isalpha:   plo     re                  ; save copy of do
           smi     'A'                 ; check uc letters
           lbnf    fails               ; jump if below A
           smi     27                  ; check upper range
           lbnf    passes              ; jump if valid
           glo     re                  ; recover character
           smi     'a'                 ; check lc letters
           lbnf    fails               ; jump if below A
           smi     27                  ; check upper range
           lbnf    passes              ; jump if valid
           lbr     fails

; **********************************
; *** check D if hex             ***
; *** Returns DF=1 - hex         ***
; ***         DF=0 - non-hex     ***
; **********************************
ishex:     sep     scall               ; see if it is numeric
           dw      isnum
           plo     re                  ; keep a copy
           lbdf    passes              ; jump if it is numeric
           smi     'A'                 ; check for below uppercase a
           lbnf    fails               ; value is not hex
           smi     6                   ; check for less then 'G'
           lbnf    passes              ; jump if so
           glo     re                  ; recover value
           smi     'a'                 ; check for lowercase a
           lbnf    fails               ; jump if not
           smi     6                   ; check for less than 'g'
           lbnf    passes              ; jump if so
           lbr     fails

;
; MOVER.ASM - Function Move Programs Into Low memory
; For Execution.
;
; Normal Entry PC = 3, RF = Program Header
;
; Exit: Starts Program at specified address
; With P=0,X=0,IE=1
;
; This module written by Richard Peters
;*********************************************************
; HEADER DEFINITION ANY ADDRESS
; OFFSET
;  00   = Program RAM Start Address
; +02   = Program RAM End Address
; +04   = Program Execution Address
; +06   = Accual PROGRAM Bytes
;*********************************************************
           org   0fadah
mover:     sex     r0
           lda     rf
           phi     r0
           lda     rf
           plo     r0
           lda     rf
           phi     r1
           lda     rf
           plo     r1
           inc     r1
           lda     rf
           phi     ra
           lda     rf
           plo     ra
moverlp:   lda     rf
           str     r0
           inc     r0
           glo     r1
           str     r0
           glo     r0
           sd
           bnz     moverlp
           ghi     r1
           str     r0
           ghi     r0
           sd
           bnz     moverlp
           ldi     3
           str     r0
           ret
           ghi     ra
           phi     r0
           glo     ra
           plo     r0
           sep     r0

           org   0fb00h
resetide:  ldi     high waitrdy        ; get address of subs
           phi     rf                  ; place into rf
           ldi     low waitrdy
           plo     rf
           sex     r2                  ; point x to stack
           mark                        ; save current P and X
           sep     rf                  ; call wait for RDY routine
           dec     r2                  ; compensate for RET increment
           glo     rd                  ; get selected drive
           shr                         ; shift over 4 bits
           shr
           shr
           shr
           stxd                        ; write drive select to stack
           ldi     6                   ; select device register
           str     r2                  ; write to stack
           out     2                   ; write select port
           out     3                   ; write device code
           dec     r2                  ; point back
           ldi     2                   ; function to perform soft reset
           stxd                        ; write to stack
           ldi     00eh                ; set interupt port
           str     r2                  ; write to stack
           out     2                   ; write select port
           out     3                   ; write reset code
           dec     r2                  ; point back
           ldi     low waitrdy
           plo     rf
           mark                        ; save current P and X
           sep     rf                  ; call wait for RDY routine
           dec     r2                  ; compensate for RET increment
           ldi     0efh                ; command to set features
           stxd                        ; store on stack
           ldi     7                   ; command register address
           stxd                        ; store on stack
           ldi     1                   ; enable 8-bit mode
           stxd                        ; store on stack
           str     r2                  ; also is feature register address
           out     2                   ; selec feacture register
           out     3                   ; select 8-bit mode
           out     2                   ; select command register
           out     3                   ; issue set feature command
           dec     r2                  ; compensate for last out
           ldi     low waitrdy
           plo     rf
           mark                        ; save current P and X
           sep     rf                  ; call wait for RDY routine
           dec     r2                  ; compensate for ret increment
           sep     sret                ; return to caller

           
wrtide:    glo     rc                  ; save consumed registers
           stxd
           ghi     rc
           stxd
           sep     scall               ; call wait til ready
           dw      waitrdy
           ldi     030h                ; command for sector write
           sep     scall               ; now setup command
           dw      wrtcmd
           ldi     2                   ; high byte of 512
           phi     rc                  ; place into count
           ldi     0                   ; low byte of 512
           plo     rc                  ; place into low of count
           ldi     0                   ; need data register
           str     r2                  ; place on stack
           out     2                   ; select data register
           dec     r2                  ; move pointer
           sex     rf                  ; set data pointer
wrtloop:   out     3                   ; write to ide controller
           dec     rc                  ; decrement byte count
           glo     rc                  ; check for completion
           bnz     wrtloop             ; jump if not
           ghi     rc                  ; need to check high byte
           bnz     wrtloop             ; jump if more to go
           sex     r2                  ; point x to stack
           sep     scall               ; call wait til ready
           dw      waitrdy
ideret:    plo     re
           irx                         ; recover consumed registers
           ldxa
           phi     rc
           ldx
           plo     rc
           glo     re
           sep     sret                ; and return to caller

rdide:     glo     rc                  ; save consumed registers
           stxd
           ghi     rc
           stxd
           sep     scall               ; call wait til ready 
           dw      waitrdy
           ldi     020h                ; command for sector read
           sep     scall               ; now setup command
           dw      wrtcmd
ide_read:  ldi     2                   ; high byte of 512
           phi     rc                  ; place into count
           ldi     0                   ; lo byte of 512
           plo     rc                  ; place into low of count
           str     r2                  ; place on stack
           out     2                   ; select data register
           dec     r2                  ; move pointer
           sex     rf                  ; set data pointer
rdloop:    inp     3                   ; read from ide controller
           inc     rf                  ; point to next position
           dec     rc                  ; decrement byte count
           glo     rc                  ; check for completion
           bnz     rdloop              ; jump if not
           ghi     rc                  ; need to check high byte
           bnz     rdloop              ; jump if more to go
           ldi     0                   ; signify read complete
           sex     r2
           br      ideret              ; return to caller
          
waitrdy:   ldi     07h                 ; need status register
           str     r2                  ; store onto stack
           out     2                   ; write ide selection port
           dec     r2                  ; point x back to free spot
           ldi     0                   ; setup timeout
           plo     rc
           phi     rc
rdyloop:   dec     rc                  ; decrement timeout
           glo     rc                  ; check for end
           bnz     rdy_go
           ghi     rc
           bnz     rdy_go
           smi     0                   ; signal timeout error
           sep     sret                ; return to caller
rdy_go:    inp     3                   ; read status port
           ani     0c0h                ; mask for BSY and RDY
           smi     040h                ; want only RDY bit
           bnz     rdyloop             ; loop back until drive is ready
           ldn     r2                  ; get status byte
           adi     0                   ; signal good
           sep     sret                ; return to caller
; RF will point to wrtcmd, which is next needed after first waitrdy
wrtcmd:    stxd                        ; write passed command to stack
           ldi     7                   ; command register
           stxd                        ; write to stack
           ghi     r8                  ; get device
           stxd                        ; write to stack
           ldi     6                   ; head/device register
           stxd                        ; write to stack
           glo     r8                  ; get high of lba
           stxd                        ; write to stack
           ldi     5                   ; cylinder high register
           stxd                        ; write to stack
           ghi     r7                  ; get mid of lba
           stxd                        ; write to stack
           ldi     4                   ; cylinder lo register
           stxd                        ; write to stack
           glo     r7                  ; get lo of lba
           stxd                        ; write to stack
           ldi     3                   ; sector start register
           stxd                        ; write to stack
           ldi     1                   ; read one sector
           stxd                        ; write to stack
           ldi     2                   ; sector count register register
           str     r2                  ; write to stack
           ldi     6                   ; 6 sets to output
wrtcmd2:   out     2
           out     3
           smi     1                   ; subtract 1
           bnz     wrtcmd2             ; loop back if not done
           dec     r2                  ; point back to free register
drqloop:   inp     3                   ; read status register
           ani     8                   ; mask for DRQ bit
           bz      drqloop             ; loop until found
           sep     sret                ; return to caller
; the branch to beforerdy, allows us to use waitrdy again

	org	0fc00h
           sep     r3
delay:     ghi     re                  ; get baud constant
           shr                         ; remove echo flag
           plo     re                  ; put into counter
           sex     r2                  ; waste a cycle
delay1:    dec     re                  ; decrement counter
           glo     re                  ; get count
           bz      delay-1             ; return if zero
           br      delay1              ; otherwise keep going

timalc:    glo     rb                  ; save consumed registesr
           stxd
           ghi     rb
           stxd
           glo     rc
           stxd
           ghi     rc
           stxd
           ldi     0                   ; zero counter 1
           phi     rc
           plo     rc
           phi     rb                  ; and counter 2
           plo     rb
           SERP    $                   ; wait until start bit found
setbd1:    inc     rc
           sex     r2
           sex     r2
           SERN    setbd1              ; wait until another high
setbd2:    inc     rb
           sex     r2
           sex     r2
           SERP    setbd2              ; wait til the next low
           glo     rb                  ; compare values
           shr                         ; quantize over small differences
           shr     
           str     r2
           glo     rc
           shr
           shr
           sm      
           bz      setbd3             ; jump if CR was entered
           glo     rb
           plo     rc
           ldi     0
           lskp    
setbd3:    ldi     1
           phi     rb
           glo     rc
           smi     4
           phi     re
           ghi     rb
           shr     
           ghi     re
           shlc    
           phi     re
           irx                         ; recover consumed registesr
           ldxa
           phi     rc
           ldxa
           plo     rc
           ldxa
           phi     rb
           ldx
           plo     rb
           sep     sret

type:      plo     re
           glo     rf
           stxd
           ghi     rf
           stxd
           glo     rd
           stxd
           ghi     rd
           stxd
           glo     re
           phi     rf
           ldi     9                   ; 9 bits to send
           plo     rf
           ldi     high delay
           phi     rd
           ldi     low delay
           plo     rd
           ldi     0
           shr
sendlp:    bdf     sendnb              ; jump if no bit
           seq                         ; set output
           br      sendct
sendnb:    req                         ; reset output
           br      sendct
sendct:    sep     rd                  ; perform bit dela
           sex r2
           sex r2
           ghi     rf
           shrc
           phi     rf
           dec     rf
           glo     rf
           bnz     sendlp
           req                         ; set stop bits
           sep     rd
           irx
           ldxa
           phi     rd
           ldxa
           plo     rd
           ldxa
           phi     rf
           ldx
           plo     rf
           sep     sret

#ifdef ELF2K
read:      glo     rf
           stxd
           ghi     rf
           stxd
           glo     rd
           stxd
           ghi     rd
           stxd
           ldi     8                   ; 8 bits to receive
           plo     rf
           ldi     high delay
           phi     rd
           ldi     low delay
           plo     rd
           ghi     re                  ; first delay is half bit size
           phi     rf
           shr
           smi     01
           phi     re
           SERP    $                   ; wait for transmission
           sep     rd                  ; wait half the pulse width
           ghi     rf                  ; recover baud constant
           phi     re
recvlp:    ghi     rf
           shr                         ; shift right
           SERN    recvlp0             ; jump if zero bi
           ori     128                 ; set bit
recvlp1:   phi     rf
           sep     rd                  ; perform bit delay
           dec     rf                  ; decrement bit count
           nop
           nop
           glo     rf                  ; check for zero
           bnz     recvlp              ; loop if not
recvdone:  req
           sep     rd                  ; get past stop bit
           sep     rd
           ghi     rf                  ; get character
           plo     re
           irx
           ldxa
           phi     rd
           ldxa
           plo     rd
           ldxa
           phi     rf
           ldx
           plo     rf
           glo     re
           shr
           plo     re                  ; save char
           ghi     re                  ; get echo flag
           shr                         ; see if need echo
           glo     re                  ; get character
           bnf     noecho              ; jump if no echo needed
           stxd                        ; save on stack
           sep     scall               ; type the character out
           dw      f_type
           irx                         ; recover character
           ldx
noecho:    sep     sret                ; and return to caller
recvlp0:   br      recvlp1             ; equalize between 0 and 1

#else
read:      glo     rf
           stxd
           ghi     rf
           stxd
           glo     rd
           stxd
           ghi     rd
           stxd
           ldi     8                   ; 8 bits to receive
           plo     rf
           ldi     high delay
           phi     rd
           ldi     low delay
           plo     rd
           ghi     re                  ; first delay is half bit size
           phi     rf
           shr
           smi     01
           phi     re
           SERP    $                   ; wait for transmission
           sep     rd                  ; wait half the pulse width
           ghi     rf                  ; recover baud constant
           phi     re
           shr
           bdf     recvlpe
recvlp:    ghi     rf
           shr                         ; shift right
           SERN    recvlp0             ; jump if zero bi
           ori     128                 ; set bit
recvlp1:   phi     rf
           sep     rd                  ; perform bit delay
           dec     rf                  ; decrement bit count
           nop
           nop
           glo     rf                  ; check for zero
           bnz     recvlp              ; loop if not
recvdone:  req
           sep     rd                  ; get past stop bit
           sep     rd
           ghi     rf                  ; get character
           plo     re
           irx
           ldxa
           phi     rd
           ldxa
           plo     rd
           ldxa
           phi     rf
           ldx
           plo     rf
           glo     re
           shr
           sep     sret                ; and return to caller
recvlp0:   br      recvlp1             ; equalize between 0 and 1

recvlpe:   ghi     rf
           shr                         ; shift right
           SERN    recvlpe0            ; jump if zero bi
           ori     128                 ; set bit
           req
recvlpe1:  phi     rf
           sep     rd                  ; perform bit delay
           dec     rf                  ; decrement bit count
           sex     r2
           sex     r2
           glo     rf                  ; check for zero
           bnz     recvlpe             ; loop if not
           br      recvdone
recvlpe0:  seq
           br      recvlpe1
#endif


brktest:   adi     0                   ; clear DF flag
           SERP    nobreak
           SERN    $
           smi     0                   ; signal break condition on serial
nobreak:   sep     sret                ; and return

; *****************************************************
; *** Serial output with 0C translation to <ESC>[2J ***
; *****************************************************
tty:       plo     re                  ; save character
           smi     0ch                 ; compare against formfeed
           bz      ttyff               ; jump if formfeed
           glo     re                  ; recover byte
ttyend:    lbr     f_tty               ; and display character
ttyff:     sep     scall               ; display vt100 sequence to clear screen
           dw      typeinmsg
           db      01bh,'[2J',0
           sep     sret                ; and return to caller


initcall:  ldi     high ret
           dec     r2
           dec     r2
           phi     r5
           ldi     low ret
           plo     r5
           ldi     high call
           phi     r4
           ldi     low call
           plo     r4
           sep     r5

         org     0fd00h
; **** Strcmp compares the strings pointing to by R(D) and R(F)
; **** Returns:
; ****    R(F) = R(D)     0
; ****    R(F) < R(D)     -1 (255)
; ****    R(F) > R(D)     1
strcmp:  lda     rd          ; get next byte in string
         ani     0ffh        ; check for zero
         bz      strcmpe     ; found end of first string
         stxd                ; store into memory
         irx
         lda     rf          ; get byte from first string
         sm                  ; subtract 2nd byte from it
         bz      strcmp      ; so far a match, keep looking
         bnf     strcmp1     ; jump if first string is smaller
         ldi     1           ; indicate first string is larger
         lskp                ; and return to caller
strcmp1: ldi     255         ; return -1, first string is smaller
         sep     sret        ; return to calelr
strcmpe: lda     rf          ; get byte from second string
         bz      strcmpm     ; jump if also zero
         ldi     1           ; first string is smaller (returns -1)
         sep     sret        ; return to caller
strcmpm: ldi     0           ; strings are a match
         sep     sret        ; return to caller

; **** ltrim trims leading white space from string pointed to by R[F]
; **** Returns:
; ****    R(F) pointing to non-whitespace portion of string
ltrim:   ldn     rf          ; get next byte from string
         lbz     return      ; return if at end of string
         smi     ' '+1       ; looking for anthing <= space
         lbdf    return      ; found first non white-space
         inc     rf          ; point to next character
         br      ltrim       ; keep looking

; **** strcpy copies string pointed to by R[F] to R[D]
strcpy:  lda    rf           ; get byte from source string
         str    rd           ; store into destination
         lbz    return       ; return if copied terminator
         inc    rd           ; increment destination pointer
         br     strcpy       ; continue looping

; **** memcpy copies R[C] bytes from R[F] to R[D]
memcpy:  glo    rc           ; get low count byte
         bnz    memcpy1      ; jump if not zero
         ghi    rc           ; get high count byte
         lbz    return       ; return if zero
memcpy1: lda    rf           ; get byte from source
         str    rd           ; store into destination
         inc    rd           ; point to next destination position
         dec    rc           ; decrement count
         br     memcpy       ; and continue copy

; *** RC:RB = RF * RD (RB is low word)
; *** R(X) must point to suitable stack
mul16:     ldi     0                   ; zero out total
           phi     rb
           plo     rb
           phi     rc
           plo     rc
           sex     r2                  ; make sure X points to stack
mulloop:   glo     rd                  ; get low of multiplier
           bnz     mulcont             ; continue multiplying if nonzero
           ghi     rd                  ; check hi byte as well
           bnz     mulcont
           sep     sret                ; return to caller
mulcont:   ghi     rd                  ; shift multiplier
           shr     
           phi     rd
           glo     rd
           shrc    
           plo     rd
           bnf     mulcont2            ; loop if no addition needed
           glo     rf                  ; add F to C:B
           str     r2
           glo     rb
           add     
           plo     rb
           ghi     rf
           str     r2
           ghi     rb
           adc
           phi     rb
           glo     rc                  ; carry into high word
           adci    0
           plo     rc
           ghi     rc
           adci    0
           phi     rc
mulcont2:  glo     rf                  ; shift first number
           shl
           plo     rf
           ghi     rf
           shlc
           phi     rf
           br      mulloop             ; loop until done

; *** RB = RF/RD
; *** RF = Remainder
; *** uses R8 and R9
div16:     ldi     0                   ; clear answer
           phi     rb
           plo     rb
           phi     r8                  ; set additive
           plo     r8
           inc     r8
           glo     rd                  ; check for divide by 0
           bnz     d16lp1
           ghi     rd
           bnz     d16lp1
           ldi     0ffh                ; return 0ffffh as div/0 error
           phi     rb
           plo     rb
           sep     sret     
d16lp1:    ghi     rd                  ; get high byte from r7
           ani     128                 ; check high bit
           bnz     divst               ; jump if set
           glo     rd                  ; lo byte of divisor
           shl                         ; multiply by 2
           plo     rd                  ; and put back
           ghi     rd                  ; get high byte of divisor
           shlc                        ; continue multiply by 2
           phi     rd                  ; and put back
           glo     r8                  ; multiply additive by 2
           shl
           plo     r8
           ghi     r8
           shlc
           phi     r8
           br      d16lp1              ; loop until high bit set in divisor
divst:     glo     r8                  ; get low of divisor
           bnz     divgo               ; jump if still nonzero
           ghi     r8                  ; check hi byte too
           lbz     return              ; jump if done
divgo:     ghi     rf                  ; copy dividend
           phi     r9
           glo     rf
           plo     r9
           glo     rd                  ; get lo of divisor
           stxd                        ; place into memory
           irx                         ; point to memory
           glo     rf                  ; get low byte of dividend
           sm                          ; subtract
           plo     rf                  ; put back into r6
           ghi     rd                  ; get hi of divisor
           stxd                        ; place into memory
           irx                         ; point to byte
           ghi     rf                  ; get hi of dividend
           smb                         ; subtract
           phi     rf                  ; and put back
           bdf     divyes              ; branch if no borrow happened
           ghi     r9                  ; recover copy
           phi     rf                  ; put back into dividend
           glo     r9
           plo     rf
           br      divno               ; jump to next iteration
divyes:    glo     r8                  ; get lo of additive
           stxd                        ; place in memory
           irx                         ; point to byte
           glo     rb                  ; get lo of answer
           add                         ; and add
           plo     rb                  ; put back
           ghi     r8                  ; get hi of additive
           stxd                        ; place into memory
           irx                         ; point to byte
           ghi     rb                  ; get hi byte of answer
           adc                         ; and continue addition
           phi     rb                  ; put back
divno:     ghi     rd                  ; get hi of divisor
           shr                         ; divide by 2
           phi     rd                  ; put back
           glo     rd                  ; get lo of divisor
           shrc                        ; continue divide by 2
           plo     rd
           ghi     r8                  ; get hi of divisor
           shr                         ; divide by 2
           phi     r8                  ; put back
           glo     r8                  ; get lo of divisor
           shrc                        ; continue divide by 2
           plo     r8
           br      divst               ; next iteration

ttyold:    lbr     tty

; *****************************************
; *** See if D is alphanumeric          ***
; *** Returns: DF=0 - not valid         ***
; ***          DF=1 - is valid          ***
; *****************************************
isalnum:   plo     re                  ; keep copy of D
           sep     scall               ; check if numeric
           dw      isnum
           lbdf    passes              ; jump if numeric
           sep     scall               ; check for alpha
           dw      isalpha
           lbdf    passes              ; jump if alpha
           lbr     fails               ; otherwise fails

; ***********************************
; *** Check for symbol terminator ***
; *** Returns: DF=1 - terminator  ***
; ***********************************
isterm:    sep     scall               ; see if alphanumeric
           dw      isalnum
           lbdf    fails               ; fails if so
           lbr     passes

         org     0fe00h
; ******************************************
; *** Check if symbol is in symbol table ***
; *** RF - pointer to ascii symbol       ***
; *** R7 - pointer to token table        ***
; *** Returns: RD - function number      ***
; ***          DF=1 - is function        ***
; ***          DF=0 - is not a function  ***
; ******************************************
tokenfunc: glo     r7                  ; save position of R7
           stxd
           ghi     r7
           stxd
           glo     rb                  ; save other consumed register
           stxd
           ghi     rb
           stxd
           ldi     0                   ; setup function number
           plo     rd
           ghi     rf                  ; save buffer position
           phi     rb
           glo     rf
           plo     rb
tfloop:    ldn     r7                  ; see if at last token
           bnz     tfgo                ; jump if not
           adi     0                   ; signal symbol not found
tfreturn:  irx                         ; recover consumed registers
           ldxa
           phi     rb
           ldxa
           plo     rb
           ldxa
           phi     r7
           ldx
           plo     r7
           sep     sret                ; and return
tfgo:      ldn     r7                  ; get token byte
           ani     080h                ; see if last one
           bnz     tflast              ; jump if it was
           lda     r7                  ; get byte from function table
           str     r2                  ; setup compare
           lda     rf                  ; get byte from input
           sm                          ; and compare
           bz      tfgo                ; loop back if match
tfnolp:    lda     r7                  ; need to find end of token
           ani     080h
           bz      tfnolp              ; loop until found
tfno:      inc     rd                  ; increment function number
           ghi     rb                  ; restore buffer position
           phi     rf
           glo     rb
           plo     rf
           br      tfloop              ; loop to check next token
tflast:    lda     r7                  ; get byte from token
           ani     07fh                ; strip high bit
           str     r2                  ; store for compare
           lda     rf                  ; get byte from buffer
           sm                          ; and see if a match
           bnz     tfno                ; jump if not
           smi     0                   ; signal match found
           br      tfreturn            ; and return

; ***********************************************
; *** identify symbol as decimal, hex, or non ***
; *** RF - pointer to symbol                  ***
; *** Returns: D=0 - decimal number           ***
; ***          D=1 - hex number               ***
; ***          DF=1 - non numeric             ***
; ***          DF=0 - is numeric              ***
; ***********************************************
idnum:     glo     rf                  ; save position
           stxd
           ghi     rf
           stxd
           ldn     rf                  ; get first byte
           sep     scall               ; must be numeric
           dw      isnum
           bdf     idlp1               ; jump if it was
idnumno:   smi     0                   ; signal non-numeric
           lskp
idnumyes:  adi     0                   ; signal numeric
           plo     re                  ; save number
           irx                         ; recover RF
           ldxa
           phi     rf
           ldx
           plo     rf
           glo     re                  ; recover number
           sep     sret                ; and return to caller
idlp1:     lda     rf                  ; get next byte
           sep     scall               ; check for symbol terminator
           dw      isterm
           bdf     iddec               ; signal decimal number
           sep     scall               ; see if char is numeric
           dw      isnum
           bdf     idlp1               ; jump if so
           dec     rf                  ; move back to char
idlp2:     lda     rf                  ; get next byte
           sep     scall               ; see if terminator
           dw      isterm
           bdf     idnumno             ; jump if term found before h
           sep     scall               ; check for hex character
           dw      ishex
           bdf     idlp2               ; loop back if so
           smi     'H'                 ; check for final H
           bz      idhex               ; jump if hex
           smi     32                  ; check for h
           bz      idhex
           br      idnumno             ; was not proper number
iddec:     ldi     0                   ; signal decimal number
           br      idnumyes            ; and return
idhex:     ldi     1                   ; signal hex number
           br      idnumyes            ; and return



; **** Find last available memory address
; **** Returns: RF - last writable address
freemem:   ldi     0         ; start from beginning of memory
           phi     rf        ; place into register
           plo     rf
           sex     r2        ; be sure x points to stack
fmemlp:    ldn     rf        ; get byte
           plo     re        ; save a copy
           xri     255       ; flip the bits
           str     rf        ; place into memory
           ldn     rf        ; retrieve from memory
           stxd              ; place into memory
           irx               ; point to previous value
           glo     re
           sm                ; and compare
           bz      fmemdn    ; jump if not different
           glo     re        ; recover byte
           str     rf        ; write back into memory
           inc     rf        ; point to next position
           glo     rf        ; check for possible wrap around
           bnz     fmemlp    ; jump if not
           ghi     rf
           bnz     fmemlp
fmemdn:    dec     rf        ; point back to last writable memory
           sep     sret      ; and return to caller

bootide:   ldi     high bootret        ; prepare for seetin call
           phi     r6
           ldi     low bootret
           plo     r6
           ldi     0                   ; setup temp stack
           phi     r2
           ldi     0f0h
           plo     r2
           sex     r2
           lbr     f_initcall
bootret:   ldi     0                   ; select master drive
           plo     rd
           sep     scall               ; reset ide drive
           dw      f_iderst
           ldi     0                   ; prepare to read sector 0
           plo     r7
           phi     r7
           plo     r8
           plo     rf
           ldi     0e0h
           phi     r8
           ldi     1
           phi     rf
           sep     scall
           dw      f_ideread
           ldi     1
           phi     r0
           ldi     0
           plo     r0
           sep     r0

input256:  ldi     1                   ; allow 256 input bytes
           phi     rc
           ldi     0
           plo     rc
           lbr     input

           org     0ff00h
f_boot:    lbr     bootide
f_type:    lbr     tty
#ifdef ELF2K
f_read:    lbr     e2k_rx
#else
f_read:    lbr     read
#endif
f_msg:     lbr     typemsg
f_typex:   lbr     return
f_input:   lbr     input256
f_strcmp:  lbr     strcmp
f_ltrim:   lbr     ltrim
f_strcpy:  lbr     strcpy
f_memcpy:  lbr     memcpy
f_wrtsec:  lbr     0
f_rdsec:   lbr     0
f_seek0:   lbr     0
f_seek:    lbr     0
f_drive:   lbr     0
f_setbd:   lbr     timalc
f_mul16:   lbr     mul16
f_div16:   lbr     div16
f_iderst:  lbr     resetide
f_idewrt:  lbr     wrtide
f_ideread: lbr     rdide
f_initcall: lbr    initcall
f_ideboot: lbr     bootide
f_hexin:   lbr     hexin
f_hexout2: lbr     hexout2
f_hexout4: lbr     hexout4
#ifdef ELF2K
f_tty:     lbr     e2k_tx
#else
f_tty:     lbr     type
#endif
f_mover:   lbr     mover
f_minimon: lbr     minimon
f_freemem: lbr     freemem
f_isnum:   lbr     isnum
f_atoi:    lbr     atoi
f_uintout: lbr     uintout
f_intout:  lbr     intout
f_inmsg:   lbr     typeinmsg
f_inputl:  lbr     input
#ifdef ELF2K
f_brktest: lbr     e2k_brk
#else
f_brktest: lbr     brktest
#endif
f_findtkn: lbr     tokenfunc
f_isalpha: lbr     isalpha
f_ishex:   lbr     ishex
f_isalnum: lbr     isalnum
f_idnum:   lbr     idnum
f_isterm:  lbr     isterm
f_getdev:  lbr     0

return:    sep     sret                ; return to caller

; ***************************************
; *** Type message pointed to by R[F] ***
; ***************************************
typemsg:   lda     rf                  ; load byte from message
           lbz     return              ; return if last byte
           sep     scall               ; call type routine
           dw      f_type
           br      typemsg             ; loop until a zero found

input:     glo     ra                  ; save RA
           stxd
           ghi     ra
           stxd
           ldi     0                   ; byte count
           plo     ra                  ; store into counter
inplp:     sep     scall               ; call input function
           dw      f_read
           plo     re                  ; save char
           smi     3                   ; check for <CTRL><C>
           bz      inpterm             ; terminate input
           smi     5                   ; check for <BS>
           bz      isbs                ; jump if so
           smi     5                   ; check for <CR>
           bz      inpdone             ; jump if so
           glo     rc                  ; check count
           bnz     inpcnt              ; jump if can continue
           ghi     rc                  ; check high of count
           bnz     inpcnt
           ldi     8                   ; performa backspace
           sep     scall
           dw      type
           br      bs2                 ; remove char from screen
inpcnt:    glo     re
           str     rf                  ; store into output
           inc     rf                  ; point to next position
           smi     08                  ; look for backspace
           bnz     nobs                ; jump if not a backspace
isbs:      glo     ra                  ; get input count
           bz      inplp               ; disregard if string is empty
           dec     ra                  ; decrement the count
           dec     rf                  ; decrement buffer position
           inc     rc                  ; increment allowed characters
bs2:       ldi     32                  ; display a space
           sep     scall
           dw      type
           ldi     8                   ; then backspace again
           sep     scall
           dw      type
           br      inplp               ; and loop back for more
nobs:      inc     ra                  ; increment input count
           dec     rc                  ; decrement character count
           br      inplp               ; and then loop back
inpdone:   ldi     0                   ; need a zero terminator
           shr                         ; reset DF flag, to show valid input
inpdone2:  str     rf                  ; store into buffer
           irx                         ; recover RA
           ldxa
           phi     ra
           ldx
           plo     ra
           sep     sret                ; return to caller
inpterm:   smi     0                   ; signal <CTRL><C> exit
           ldi     0                   ; finish
           br      inpdone2
          

; *****************************************************
; *** Function to implement a stack based call      ***
; ***    RX is assumed to be the stack pointer      ***
; ***    R3 is assumed to be the main PC            ***
; ***    R6 will hold return address                ***
; ***    usage is:    sep R4                        ***
; ***                 dw  call_addr                 ***
; *****************************************************
         org     0ffe0h
         lbr     call
         org     0fff1h
         lbr     ret

         org     0fff9h
version: db      1,0,3
chsum:   db      0,0,0,0

